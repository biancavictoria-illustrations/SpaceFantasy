title: PlayerStart
tags:
---
<<set $runNum to GetCurrentRunNum()>>
// <<set $stellanCommTriggered to StellanCommTriggered()>>
<<set $epilogueTriggered to EpilogueTriggered()>>
<<set $timeLichDeathDialogueTriggered to TimeLichDeathDialogueTriggered()>>

// If we just killed beetle boi
<<if StellanCommTriggered()>>
    [[PlayerStellanCommTriggered]]

// If we just picked up the captain's log on run 1
<<elseif CaptainsLogDialogueTriggered()>>
    [[PlayerCaptainsLogDialogueTriggered]]

// If we killed the time lich for the last time
<<elseif $timeLichDeathDialogueTriggered and $epilogueTriggered>>
    [[PlayerTimeLichDeathDialogueTriggeredFINAL]]

// If we killed the time lich any old time
<<elseif $timeLichDeathDialogueTriggered>>
    [[PlayerTimeLichDeathDialogueTriggered]]

// If we took the elevator back to the abandoned hub
<<elseif $epilogueTriggered>>
    [[PlayerEpilogueIntro]]

// Starting stuff
<<elseif $runNum is 1 and not Visited("PlayerFirstRunIntro")>>
    [[PlayerFirstRunIntro]]
<<elseif $runNum is 2 and not Visited("PlayerSecondRunIntro")>>
    [[PlayerSecondRunIntro]]
<<else>>
    ERROR: No player dialogue found.
<<endif>>
===



title: PlayerFirstRunIntro
tags:
---
<<SetSpeaker Player>>
Huh. Not quite as abandoned as it looked from the outside.
Ah, well. As long as the loot's good.
===

title: PlayerSecondRunIntro
tags:
---
<<SetSpeaker Player shocked>><<Emote Player surprise>>
What...?
I feel... different.
===

title: PlayerCaptainsLogDialogueTriggered
tags:
---
<<SetSpeaker Player>>
The Captain's Log...
Looks pretty old, but maybe it has a map.
===



title: PlayerStellanCommTriggered
tags:
---
<<if Visited("PlayerStellanCommTriggered")>>
    [[PlayerStellanCommTriggered1]]
<<elseif $runNum > 2>>  // Normal
    <<SetSpeaker Player>>
    Huh. A comm from Stellan. ... Answer comm.
    <<SetSpeaker Stellan comm>>
    I'm surprised you've even made it this far, Atlan.
    <<SetSpeaker Player>>
    I think you mean <i>impressed.</i>
    <<SetSpeaker Stellan comm>>
    Take the elevator up, if you feel ready for the real challenge. But I have a feeling I will be seeing you face-to-face again soon.
<<elseif $runNum is 1>>  // If you've never met Stellan before (first run)
    <<SetSpeaker Player confused>><<Emote Player question>>
    I'm getting a comm? From who? ... Answer comm.
    <<SetSpeaker Stellan comm ???>>
    Not bad. Take the elevator up, if you feel ready for the real challenge. But I have a feeling I will be meeting you face-to-face soon.
    <<SetSpeaker Player confused>>
    Huh. Maybe he's the Wizard of this tower.
<<else>>    // Second run
    <<SetSpeaker Player confused>><<Emote Player question>>
    I'm getting a comm? From who? ... Answer comm.
    <<SetSpeaker Stellan comm ???>>
    Not bad. Take the elevator up, if you feel ready for the real challenge. But I have a feeling I will be seeing you face-to-face soon.
    <<SetSpeaker Player confused>>
    Huh.
<<endif>>
===

title: PlayerStellanCommTriggered1
tags:
---
<<if Visited("PlayerStellanCommTriggered1")>>
    [[PlayerStellanCommTriggeredRepeatable]]
<<elseif $runNum is 2 and FirstClearRunNumber() < 1>>   // If it's your second run and you HAVEN'T CLEARED YET, you still don't know Stellan's name
    <<SetSpeaker Player>>
    Him again... Answer comm.
    <<SetSpeaker Stellan comm ???>>
    Same as last time: Take the elevator up.
    <<SetSpeaker Stellan commSad ???>>
    ... See you soon.
<<else>>
    <<SetSpeaker Player>>
    Answer comm.
    <<SetSpeaker Stellan comm>>
    Same as last time: Take the elevator up.
    <<SetSpeaker Stellan commSad>>
    ... See you soon.
<<endif>>
===

title: PlayerStellanCommTriggeredRepeatable
tags:
---
// Get a random number 0-3
<<set $num to RandomNum(4)>>
<<if $num is 0>>
    <<SetSpeaker Stellan comm>>
    Same as always. Take the elevator up.
<<elseif $num is 1>>
    <<SetSpeaker Stellan comm>>
    He's waiting for you. Go find the elevator.
<<elseif $num is 2>>
    <<SetSpeaker Stellan commSad>>
    Take the elevator up. ... I'll see you soon.
<<else>>
    <<SetSpeaker Stellan commSad>>
    Go on, then. Up the elevator to another pointless death.
<<endif>>
===



title: PlayerTimeLichDeathDialogueTriggered
tags:
---
<<if Visited("PlayerTimeLichDeathDialogueTriggered")>>
    [[PlayerTimeLichDeathDialogueTriggered1]]
<<elseif $runNum is 1>> // If you killed him for the first time on your FIRST run
    <<SetSpeaker TimeLich angry>><<Emote TimeLich question>>
    <i>Uugh</i>... How did you...?
    <<SetSpeaker Player>>
    I thought you said you'd be more of a challenge, old man.
    It seems your infinite time has made you weak.
    <<SetSpeaker TimeLich angry>>
    You know... nothing... of what is truly at stake...
<<else>>
    <<SetSpeaker TimeLich angry>><<Emote TimeLich question>>
    <i>Uugh</i>... How did you...?
    <<SetSpeaker Player>>
    Your reign here is over. I'm getting out of this cursed tower.
    <<SetSpeaker TimeLich angry>>
    It is not... time...
<<endif>>
===

title: PlayerTimeLichDeathDialogueTriggered1
tags:
---
<<if Visited("PlayerTimeLichDeathDialogueTriggered1")>>
    [[PlayerTimeLichDeathDialogueTriggeredRepeatable]]
<<else>>
    <<SetSpeaker TimeLich angry>><<Emote TimeLich question>>
    You... again...
    <<SetSpeaker Player>>
    As many times as it takes.
    <<SetSpeaker TimeLich angry>>
    I will see you... again... then...
<<endif>>
===

title: PlayerTimeLichDeathDialogueTriggeredRepeatable
tags:
---
// Get a random number 0-4
<<set $num to RandomNum(5)>>
<<if $num is 0>>
    <<SetSpeaker TimeLich angry>>
    ... Again... then...
<<elseif $num is 1>>
    <<SetSpeaker TimeLich angry>>
    <i>Uugh</i>... I will see you... again...
<<elseif $num is 2>>
    <<SetSpeaker TimeLich angry>>
    It is not... time...
<<elseif $num is 3>>
    <<SetSpeaker TimeLich angry>><<Emote TimeLich angry>>
    You cannot... kill... time itself...
<<else>>
    <<SetSpeaker TimeLich angry>><<Emote TimeLich angry>>
    Accept... your fate...
<<endif>>
===

title: PlayerTimeLichDeathDialogueTriggeredFINAL
tags:
---
<<SetSpeaker TimeLich angry>><<Emote TimeLich angry>>
<i>Uugh!</i>
<<Emote TimeLich question>>
... How...?
<<SetSpeaker Player>>
All the time in the world made you complacent, old man.
<<SetSpeaker TimeLich angry>>
At last... you achieved your goal...
... Freedom... Power...
But no ambition... is without consequence...
===



title: PlayerEpilogueIntro
tags:
---
<<SetSpeaker Player>>
Stellan, I did it! He's gone, we can-
<<SetSpeaker Player confused>>
...
... Stellan?
===