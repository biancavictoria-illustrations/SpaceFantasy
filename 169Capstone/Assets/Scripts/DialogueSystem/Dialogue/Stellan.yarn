title: StellanStart
tags:
---
<<if EpilogueTriggered()>>
    [[StellanEpilogueDialogue]]
<<endif>>

<<set $firstClearRunNumber to FirstClearRunNumber()>>
<<set $completedRuns to GetNumberOfCompletedRuns()>>

<<set $headNode to SelectNextNode()>>   // Needs to be here to account for time lich stuff

<<set $visitedDefault to Visited("StellanDefaultDialogue")>>
<<set $visitedDefault1 to Visited("StellanDefaultDialogue1")>>
<<set $visitedDefault2 to Visited("StellanDefaultDialogue2")>>
<<set $visitedDefault3 to Visited("StellanDefaultDialogue3")>>
<<if $visitedDefault and $visitedDefault1 and $visitedDefault2 and $visitedDefault3>>
    [[StellanNodeSelectHub]]
<<elseif $visitedDefault2>>
    [[StellanDefaultDialogue3]]
<<elseif $visitedDefault1>>
    [[StellanDefaultDialogue2]]
<<elseif $visitedDefault>>
    [[StellanDefaultDialogue1]]
<<else>>
    [[StellanDefaultDialogue]]
<<endif>>
===

title: StellanNodeSelectHub
tags:
---
<<if EpilogueTriggered()>>
    [[StellanEpilogueDialogue]]
<<endif>>

// === Enemy Killed ===

<<if $headNode is "EnemyKilledBeetleBoss">> // No matter what, go to the "killed by" branch
    [[StellanKilledByBeetleBoss]]
<<elseif $headNode is "EnemyKilledTimeLich">>
    [[StellanEnemyKilledTimeLich]]

// === Killed By ===

<<elseif $headNode is "KilledByTimeLich">>
    <<if Visited("StellanEnemyKilledTimeLich") and not Visited("StellanDialogueCompletedStellanEnemyKilledTimeLich")>>
        // If you killed the lich before and haven't yet seen the second dialogue in that branch, play that instead of continuing the KilledByTimeLich dialogue
        [[StellanDialogueCompletedStellanEnemyKilledTimeLich]]
    <<else>>
        [[StellanKilledByTimeLich]]
    <<endif>>

<<elseif $headNode is "KilledByDeathPitTimeLichArena">>
    <<if Visited("StellanEnemyKilledTimeLich") and not Visited("StellanDialogueCompletedStellanEnemyKilledTimeLich")>>
        // If you killed the lich before and haven't yet seen the second dialogue in that branch, play that instead of continuing the KilledByDeathPitTimeLichArena dialogue
        [[StellanDialogueCompletedStellanEnemyKilledTimeLich]]
    <<else>>
        [[StellanKilledByDeathPitTimeLichArena]]
    <<endif>>

<<elseif $headNode is "KilledByDeathPit">>
    [[StellanKilledByDeathPit]]
<<elseif $headNode is "KilledBySlime">>
    [[StellanKilledBySlime]]
<<elseif $headNode is "KilledByBeetleBoss">>
    [[StellanKilledByBeetleBoss]]

// === Dialogue Completed ===

<<elseif $headNode is "DialogueCompletedDoctorDefaultDialogue5">>
    [[StellanDialogueCompletedDoctorDefaultDialogue5]]

<<elseif $headNode is "DialogueCompletedStellanEnemyKilledTimeLich">>
    [[StellanDialogueCompletedStellanEnemyKilledTimeLich]]

<<elseif $headNode is "DialogueCompletedBrynDefaultDialogue">>
    [[StellanDialogueCompletedBrynDefaultDialogue]]
<<elseif $headNode is "DialogueCompletedRhianDefaultDialogue1">>
    [[StellanDialogueCompletedRhianDefaultDialogue1]]

// === Generic Stuff ===

<<elseif $headNode is "NumRuns">>
    [[StellanNumRuns]]
<<elseif $headNode is "DefaultDialogue">>
    [[StellanDefaultDialogue]]
<<elseif $headNode is "Repeatable">>
    [[StellanRepeatable]]

<<else>>
    [[StellanRepeatable]]
    ERROR: No head node found for {$headNode}!
<<endif>>
===



title: StellanDialogueCompletedBrynDefaultDialogue
tags:
---
<<if Visited("StellanDialogueCompletedBrynDefaultDialogue")>>
    [[StellanDialogueCompletedBrynDefaultDialogue1]]
<<else>>
    <<SetSpeaker Player>>
    So, Bryn - the engineer - what's his deal?
    <<SetSpeaker Stellan>>
    His "deal"?
    <<SetSpeaker Player>>
    Yeah, I mean... he seems nice and all, but... Y'know. What's his <i>deal</i>?
    <<SetSpeaker Stellan>>
    Are you so shocked that someone might actually, genuinely just be a kind person?
    The criminal life of thievery clearly has taken its toll on you.
<<endif>>
===

title: StellanDialogueCompletedBrynDefaultDialogue1
tags:
---
<<SetSpeaker Stellan>><<BranchComplete DialogueCompleted DialogueCompletedBrynDefaultDialogue>>
Bryn is... joyful. And kind. And outgoing.
<<Emote Stellan angry>>
So incredibly, <i>annoyingly</i> outgoing.
If he so much as hears you walk by his station, you cannot avoid a conversation with him.
... But he always has something uplifting to say.
<<SetSpeaker Player>>
Is that... genuine fondness in your voice? Coming from <i>you</i>, Stellan?
<<SetSpeaker Stellan>> // Rolls eyes
... Perhaps. Don't make me take it back.
===



title: StellanDialogueCompletedRhianDefaultDialogue1
tags:
---
<<if Visited("StellanDialogueCompletedRhianDefaultDialogue1")>>
    [[StellanDialogueCompletedRhianDefaultDialogue11]]
<<else>>
    <<SetSpeaker Player>>
    You know a "Rhian" on this ship?
    <<SetSpeaker Stellan>>
    Ah, so you met our esteemed weapons specialist. She is... certainly <i>enthusiastic</i> about her work.
    Be sure to stay on her good side. She has a habit of using those she is less fond of as target practice.
    <<SetSpeaker Player>>
    Speaking from personal experience on that one?
    <<SetSpeaker Stellan>>
    Nonsense. She did, however, once light my cloak on fire.
    <<Emote Stellan sad>>
    I believe that may be a sign of affection, coming from her.
<<endif>>
===

title: StellanDialogueCompletedRhianDefaultDialogue11
tags:
---
<<if Visited("StellanDialogueCompletedRhianDefaultDialogue11")>>
    [[StellanDialogueCompletedRhianDefaultDialogue12]]
<<else>>
    <<SetSpeaker Player>>
    So... Rhian once set your cloak on fire? I have to hear more.
    <<SetSpeaker Stellan>>
    She used to visit my lab and "help" with my arcane research.
    In her case, "helping" meant "setting various substances on fire," or combining two arcane reagents that I <i>explicitly</i> told her not to combine, just to see what dangers I had been hiding from her.
    <<SetSpeaker Player>>
    Sounds like Rhi.
    <<SetSpeaker Stellan>><<Emote Stellan surprise>>
    "Rhi"?
    You're getting awfully comfortable on this ship, Atlan.
    <<SetSpeaker Player>>
    Can't fault a man for trying to make the best of a bad situation.
    <<SetSpeaker Stellan sad>>
    ... Be cautious of getting too attached.
<<endif>>
===

title: StellanDialogueCompletedRhianDefaultDialogue12
tags:
---
<<SetSpeaker Player>><<BranchComplete DialogueCompleted DialogueCompletedRhianDefaultDialogue1>>
You were close with them. The other crew members.
<<SetSpeaker Stellan>>
They were my coworkers-
<<SetSpeaker Player angry>><<Emote Player angry>>
Shut up, Stellan, you clearly speak of them as <i>friends</i>.
<<SetSpeaker Player confused>>
Don't try to act like you've always been this detached from everyone and everything on this ship.
You were the <i>Captain's apprentice</i>. You must have known... everyone.
<<SetSpeaker Stellan sad>>
...
<<SetSpeaker Stellan>>
A lot has happened since then.
===



title: StellanDialogueCompletedDoctorDefaultDialogue5
tags:
---
<<if Visited("StellanDialogueCompletedDoctorDefaultDialogue5")>>
    [[StellanDialogueCompletedDoctorDefaultDialogue51]]
<<else>>
    <<SetSpeaker Player>>
    You ever heard of the <i>caelarae?</i>
    <<SetSpeaker Stellan>>
    Taking up an interest in children’s stories, are you?
    <<SetSpeaker Player>>
    An interest in family history, actually.
    <<SetSpeaker Stellan>>
    As entertaining as hearing about your latest delusions of grandeur may be, we’re both very busy individuals. Let’s make our exchange and be done with this.
<<endif>>
===

title: StellanDialogueCompletedDoctorDefaultDialogue51
tags:
---
<<if Visited("StellanDialogueCompletedDoctorDefaultDialogue51")>>
    [[StellanDialogueCompletedDoctorDefaultDialogue52]]
<<else>>
    <<SetSpeaker Player>>
    Are you familiar with Dr. Sanguin?
    <<SetSpeaker Stellan>>
    I’ve met the resident maniac, yes. What of him?
    <<SetSpeaker Player>>
    He claims to have found traces of caelarae ancestry in my blood.
    <<SetSpeaker Stellan>>
    He also claims to be a doctor. Perhaps learn not to be so gullible when talking to strangers aboard a cursed ship, Atlan.
    Oh, and the singular is <i>caelara.</i>
<<endif>>
===

title: StellanDialogueCompletedDoctorDefaultDialogue52
tags:
---
<<if Visited("StellanDialogueCompletedDoctorDefaultDialogue52")>>
    [[StellanDialogueCompletedDoctorDefaultDialogue53]]
<<else>>
    <<SetSpeaker Stellan>>
    The caelarae are a myth. Stories told to elven children of sentient stars who watch over them in the night. Guardians. Protectors.
    I was named after one by my elven mother – supposedly, Stellan the caelara would watch over me when she left me to the mercy of my human father.
    <<Emote Stellan angry>>
    Little good that did me.
    <<SetSpeaker Player confused>>
    You’re certain they’re a myth?
    <<SetSpeaker Stellan>>
    I was.
<<endif>>
===

title: StellanDialogueCompletedDoctorDefaultDialogue53
tags:
---
<<if Visited("StellanDialogueCompletedDoctorDefaultDialogue53")>>
    [[StellanDialogueCompletedDoctorDefaultDialogue54]]
<<else>>
    <<SetSpeaker Player>>
    I’ve been on my own for most of my life. But I have vague memories of my mother telling me my eyes were full of golden starlight.
    <<SetSpeaker Stellan>>
    Drivel. If you truly are descended from a caelara, surely the bloodline is so diluted that it hardly matters–
    <<SetSpeaker Player angry>><<Emote Player question>>
    Not a chance that you believe that. Tell me, Stellan, what exactly have you been doing to me?
    You once told me your magic only unlocks hidden potential. And these Star Shards you use to do it – you mean to tell me they’re not connected to me being descended from a <i>sentient star?</i>
    <<SetSpeaker Stellan sad>>
    … There may be a correlation.
    <<SetSpeaker Stellan>>
    ... I will share with you my research notes, if you truly wish to see them. You can access them in the old Captain's Log you carry around.
<<endif>>
===

title: StellanDialogueCompletedDoctorDefaultDialogue54
tags:
---
<<SetSpeaker Player angry>><<Emote Player angry>><<BranchComplete DialogueCompleted DialogueCompletedDoctorDefaultDialogue5>>
So you’ve been experimenting on me this whole time? You suspected my bloodline and wanted to see what would happen if you used those Star Shards on me?
<<SetSpeaker Stellan>>
I may have suspected certain things. But if I was wrong, it’s not like it would have hurt you.
<<SetSpeaker Player angry>>
No? And what would have happened, if you had been wrong?
<<SetSpeaker Stellan>>
Remember when I mentioned you burning up with arcane energy?
<<SetSpeaker Player angry>>
So it would have killed me.
<<SetSpeaker Stellan>>
We’re in a time loop, Atlan. You would have come back.
===



title: StellanEnemyKilledTimeLich
tags:
---
<<BranchComplete EnemyKilled EnemyKilledTimeLich>>
<<if $firstClearRunNumber > 0 and $firstClearRunNumber < 5>>
    <<SetSpeaker Stellan>>
    Well, that is certainly a concerning look. Tell me, what new horrors did you uncover on this ship-
    <<SetSpeaker Player angry>><<Emote Player angry>>
    How many times must I kill the Lich before you tell me what's really going on here?
    Why does everything still reset, even when I beat him? Why are we still here? If you know something, if you’re <i>hiding</i> something–
    <<SetSpeaker Stellan>>  // rolling eyes, for the first one
    Tone down the aggression, Atlan.
    … I have some theories, but I’ll need some time. And lots of Star Shards.
<<else>>
    <<SetSpeaker Stellan>>
    Well, that is certainly a concerning look. Tell me, what new horrors did you uncover on this ship, the likes of which presumably killed you in inventively horrific ways?
    <<SetSpeaker Player confused>>
    I killed him.
    <<SetSpeaker Stellan>><<Emote Stellan surprise>>
    ...
    ... What?
    <<SetSpeaker Player confused>>
    I killed the Lich.
    <<SetSpeaker Player angry>><<Emote Player angry>>
    Why did everything reset? Why are we still here? What’s going on? If you know something, if you’re <i>hiding</i> something–
    <<SetSpeaker Stellan>>  // rolling eyes, for the first one
    Tone down the aggression, Atlan. You’ve only made it this far because of me.
    … I have some theories, but I’ll need some time. And lots of Star Shards.
<<endif>>
===

title: StellanDialogueCompletedStellanEnemyKilledTimeLich
tags:
--- // What if you die to him next time? will it play killedby instead of this?
<<BranchComplete DialogueCompleted DialogueCompletedStellanEnemyKilledTimeLich>>
<<if $firstClearRunNumber > 0 and $firstClearRunNumber < 5>>
    <<SetSpeaker Player angry>>
    You didn’t actually think I could do it. Whatever these theories of yours are... you never shared them because you didn't think I was capable of breaking us out of here.
    You’ve been sending me to die over and over, just to collect these Star Shards for you.
<<else>>
    <<SetSpeaker Player angry>>
    You didn’t actually think I could do it. You’ve been sending me to die over and over, just to collect these Star Shards for you.
<<endif>>
<<SetSpeaker Stellan>>
I’ve never exactly hid my skepticism of your capabilities, nor have I tried to give you false hope.
<<SetSpeaker Player confused>>
You’re cruel.
<<SetSpeaker Stellan>>
I’m pragmatic.
<<SetSpeaker Stellan sad>>
… And for what it’s worth, you might have given me just a bit of hope, now that I've seen your persistence. So let me help you stop all this.
===



title: StellanKilledByDeathPitTimeLichArena
tags:
---
// If this was your first time facing the lich (and you died), play default lich death 1 first instead
<<if not Visited("StellanKilledByTimeLich") and $firstClearRunNumber is -1>>
    [[StellanKilledByTimeLich]]
<<else>>    // Otherwise, do this
    <<BranchComplete KilledBy KilledByDeathPitTimeLichArena>>
    // If you cleared within first four runs
    <<if $firstClearRunNumber > 0 and $firstClearRunNumber < 5>>
        <<SetSpeaker Stellan>>
        Atlan, you-
        You fell in a slime pit.
        <<Emote Stellan angry>>
        You bested the Lich as soon as you arrived here, and now you- you slip and fall and <i>that</i> is your glorious death?
    <<else>>    // Otherwise
        <<SetSpeaker Stellan>><<Emote Stellan angry>>
        You were <i>right there,</i> Atlan! And you... you fell in a <i>pit</i>? You <i>absolute fool</i>, how could you let this happen?
    <<endif>>   // Play regardless
    <<SetSpeaker Player angry>><<Emote Player surprise>>
    Hey, do you have any idea how overwhelming facing him can be? I had a lot to be dealing with, so yeah, I slipped up!
    I don't see you trying to go up against him.
    <<SetSpeaker Stellan sad>>
    I have tried. I have tried more than you could even imagine to escape this fate.
    <<SetSpeaker Stellan>>
    And now?
    <i>You</i> might have a real shot at breaking this loop.
    Don't you dare waste it.
<<endif>>
===



title: StellanKilledByTimeLich
tags:
---
<<if Visited("StellanKilledByTimeLich")>>
    [[StellanKilledByTimeLich1]]
<<elseif $firstClearRunNumber > 0 and $firstClearRunNumber < 5>>
    <<SetSpeaker Stellan>>
    I know that look. Not so lucky, that time.
    <<SetSpeaker Player>>
    I can take him. I've done it before.
    <<SetSpeaker Stellan>>
    You're a fool. You caught him off guard initially, nothing more. But unfortunately, you’re the best option we’ve got. So here, let me see if I can help you not get killed as quickly next time.
<<elseif Visited("StellanEnemyKilledTimeLich")>> // If you've killed the lich before and just now died to him for the first time
    <<SetSpeaker Stellan>><<Emote Stellan angry>>
    You beat him on your first try, and <i>now</i> you fail? You are growing careless, Atlan.
    <<SetSpeaker Player>>
    I can take him. I've done it before.
    <<SetSpeaker Stellan>>
    You're a fool. You caught him off guard initially, nothing more. But unfortunately, you’re the best option we’ve got. So here, let me see if I can help you not get killed as quickly next time.
<<else>>
    <<SetSpeaker Player>>
    Fought the Lich.
    <<SetSpeaker Stellan>>
    … I’m sorry. I can assume how it went.
    <<SetSpeaker Player>>
    I still think I can take him.
    <<SetSpeaker Stellan>>
    I still think you might be a fool. But unfortunately, you’re the best option we’ve got. So here, let me see if I can help you not get killed as quickly next time.
<<endif>>
<<SetSpeaker Stellan>>
And Atlan? Promise me something.
<<SetSpeaker Stellan sad>>
If you <i>do</i> kill him... make him suffer. Make him suffer for what he's done.
<<SetSpeaker Player>>
That’s an easy promise to make, my friend.
===

title: StellanKilledByTimeLich1
tags:
---
<<if Visited("StellanKilledByTimeLich1")>>
    [[StellanKilledByTimeLich2]]
<<elseif Visited("StellanEnemyKilledTimeLich")>> // If you've killed the lich before, skip this one
    [[StellanKilledByTimeLich2]]
<<else>>
    <<SetSpeaker Player>>
    Stellan, you’ve been on this ship for… quite some time.
    <<SetSpeaker Stellan>>
    To say the least.
    <<SetSpeaker Player>>
    You must know, what kind of weaknesses does the Lich have? What can I exploit? How do I beat him at his own game?
    <<SetSpeaker Stellan sad>>
    ...
    <<SetSpeaker Stellan>>
    You’re right. I do know about him.
    I know that he will never, ever go easy on you. He will never leave you an opening you can exploit.
    He is relentless. He has no weaknesses, only power. It has consumed him.
    <<SetSpeaker Player>><<Emote Player sad>>
    … Great.
<<endif>>
===

title: StellanKilledByTimeLich2
tags:
---
<<if Visited("StellanKilledByTimeLich2")>>
    [[StellanKilledByTimeLich3]]
<<else>>
    <<SetSpeaker Stellan sad>>
    Atlan–
    <<SetSpeaker Stellan>>
    … You should know. He’s not who you think he is.
    <<SetSpeaker Player>>
    What, are you about to tell me the Lich is secretly just <i>misunderstood?</i>
    <<SetSpeaker Stellan>><<Emote Stellan angry>>
    No, Atlan, listen to me.
    He wasn’t the Captain. This wasn’t his tower, originally. He took it. He killed the Wizard.
    <<SetSpeaker Player>>
    I don’t see how that changes much, personally.
    <<SetSpeaker Stellan>>
    Atlan, I have seen your desire for loot. For wealth. For renown throughout the galaxy. I tell you this to warn you.
    Be cautious of ambition.
    <<SetSpeaker Stellan sad>>
    It... changes a person.
    <<SetSpeaker Stellan>>
    Ambition alone has the power to twist a person into a monstrous shell of what he once was.
<<endif>>
===

title: StellanKilledByTimeLich3
tags:
---
<<SetSpeaker Player>><<BranchComplete KilledBy KilledByTimeLich>>
What was the Lich like before all this? <i>Who</i> was he, if not the Captain?
<<SetSpeaker Stellan>>
The same as you and me, and many others on this ship. Nothing but a kid with ambition.
<<SetSpeaker Player>>
Did you know him personally?
<<SetSpeaker Stellan sad>>
Once.
<<SetSpeaker Player confused>>
… I’m sorry.
===



title: StellanKilledByBeetleBoss
tags:
---
<<if Visited("StellanKilledByBeetleBoss")>>
    [[StellanKilledByBeetleBoss1]]
<<else>>
    <<SetSpeaker Player>>
    So the six-legged creature. Are you going to explain to me what layer of hell that monstrosity crawled up from?
    <<SetSpeaker Stellan>>
    The Brute Pest. Once an experiment - failed or successful, who can tell?
    Now, just a sad, pathetic monster left to rot for eternity. A reminder of what happens when ambition is left unchecked.
<<endif>>
===

title: StellanKilledByBeetleBoss1
tags:
---
<<if Visited("StellanKilledByBeetleBoss1")>>
    [[StellanKilledByBeetleBoss2]]
<<else>>
    <<SetSpeaker Player>>
    That six-legged monster - the Brute Pest, you called it. It was an experiment? Experimenting with <i>what</i>?
    <<SetSpeaker Stellan>>
    Stretching the bounds of the arcane and the biological by individuals playing at being gods. Just because they could.
    <<SetSpeaker Player confused>>
    What was it before it became... whatever it is now?
    <<SetSpeaker Stellan sad>>
    <i>Who</i>.
    <<SetSpeaker Stellan>>
    You mean <i>who</i> was it before.
<<endif>>
===

title: StellanKilledByBeetleBoss2
tags:
---
<<SetSpeaker Player>><<BranchComplete KilledBy KilledByBeetleBoss>><<BranchComplete EnemyKilled EnemyKilledBeetleBoss>>
So are you ever going to tell me? Who <i>was</i> the Brute Pest?
<<SetSpeaker Stellan>><<Emote Stellan angry>>
Does it matter anymore? Do you understand why I'm telling you any of this at all?
You need to understand, Atlan. The Lich is the real monster here. All he is capable of is what you see in that sad, pathetic creature.
Destruction and torment disguised as abominations of supposed discovery. The Lich cannot be stopped.
<<SetSpeaker Player>>
We'll see about that-
<<SetSpeaker Stellan>>
No, Atlan. I mean he cannot help himself. Experimentation, stretching boundaries beyond where anyone is meant to go... It's his nature.
It's how he became what he is. It's why we're all stuck here. It's what led to those monsters out there that you kill over and over... Yet they all come right back.
<<SetSpeaker Stellan sad>>
Even now, he...
<<SetSpeaker Stellan>>
He has achieved his unbridled power and seen what emptiness it has brought him, yet he keeps pushing boundaries.
<<SetSpeaker Stellan sad>>
... And for what?
===



title: StellanKilledBySlime
tags:
---
<<if Visited("StellanKilledBySlime")>>
    [[StellanKilledBySlime1]]
<<else>>
    <<SetSpeaker Stellan>>
    Is that… residual slime ooze clinging to your sleeve? You let yourself be killed by a slime?
    A <i>slime</i>, Atlan?
    <<SetSpeaker Player>><<Emote Player angry>>
    A particularly <i>ferocious</i> slime, yes, Stellan. Your point?
    <<SetSpeaker Stellan>>
    You still think you stand a chance against the <i>Lich</i>?
<<endif>>
===

title: StellanKilledBySlime1
tags:
---
<<SetSpeaker Stellan>><<Emote Stellan question>><<BranchComplete KilledBy KilledBySlime>>
Really, Atlan? Really? A slime. Again?
<<SetSpeaker Player>>
Have you ever actually encountered one of those things? They may seem harmless, but you know they’re vicious, right?
<<SetSpeaker Stellan>>
My point remains that if you cannot handle yourself against a simple ooze monster a third your size, I highly doubt that you are the ever-so skilled adventurer you claim to be.
===

title: StellanKilledByDeathPit
tags:
---
<<SetSpeaker Stellan>><<BranchComplete KilledBy KilledByDeathPit>>
Atlan?
<<SetSpeaker Player>>
Yes, Stellan?
<<SetSpeaker Stellan>><<Emote Stellan angry>>
You fell in a slime pit, Atlan.
<<SetSpeaker Player>>
Yes, well, your work place is rather unsafe. There are violations <i>everywhere.</i>
I mean, really, would it kill you guys to put up a wet floor sign around those things?
<<SetSpeaker Stellan>>
...
Putting up with you is going to be the death of me. <i>Be more careful,</i> Atlan.
===



title: StellanNumRuns
tags:
---
<<set $currentRunNum to GetCurrentRunNum()>>
<<if $currentRunNum > 14>>
    [[StellanNumRuns15]]
<<else>>
    <<if Visited("StellanNumRuns")>>
        [[StellanRepeatable]]
    <<else>>
        <<SetSpeaker Stellan>>
        You must be quite the optimist, to keep trying like this. Either that, or just plain stupid with a magnificent death wish.
        I haven’t quite figured out which yet.
        <<SetSpeaker Player>>
        Let me know once you do.
    <<endif>>
<<endif>>
===

title: StellanNumRuns15
tags:
---
<<if $currentRunNum > 30>>
    [[StellanNumRuns34]]
<<else>>
    <<if Visited("StellanNumRuns15")>>
        [[StellanRepeatable]]
    <<else>>
        <<SetSpeaker Stellan>>
        I’m getting tired of this existence, Atlan.
        <<SetSpeaker Player>>
        Then help me set you free.
        <<SetSpeaker Stellan>>
        … Only time will tell if you can.
    <<endif>>
<<endif>>
===

title: StellanNumRuns34
tags:
---
// <<set $runNum to GetNumberOfCompletedRuns()>>
<<SetSpeaker Stellan>><<BranchComplete NumRuns NumRuns>>
Have you thought about going any faster? I am exceedingly bored of this ship.
<<SetSpeaker Player>>
I have gotten myself killed 30 times trying to save your ass, and this is how you thank me for my sacrifice?
<<SetSpeaker Stellan sad>>
... {$completedRuns}.
<<SetSpeaker Player>><<Emote Player surprise>>
… What?
<<SetSpeaker Stellan>>
You have gotten yourself killed {$completedRuns} times. Not 30.
<<SetSpeaker Player>>
… You're keeping score?
===



title: StellanDefaultDialogue
tags:
---
<<if Visited("StellanDefaultDialogue")>>
    [[StellanDefaultDialogue1]]
<<elseif $firstClearRunNumber is 1>>    // This dialogue right after first clear
    <<SetSpeaker Stellan neutral ???>><<Emote Stellan question>>
    How did you...?
    <<SetSpeaker Player>><<Emote Player surprise>>
    <i>Uugh</i>... I'm... alive?
    <<SetSpeaker Stellan neutral ???>>
    Yes, yes, it's a time loop. How miraculous and novel. Even death won't set us free.
    Now, tell me. How did you <i>do</i> that?
    <<SetSpeaker Player>>
    What, kill the skeleton man?
    <<SetSpeaker Stellan neutral ???>>
    Lich.
    You killed the Lich.
    You, some... wayward adventurer. A pirate. A mere <i>scoundrel,</i> and you board this ship out of <i>nowhere</i> and kill the Lich. Who <i>are</i> you?
    <<SetSpeaker Player>>
    Name's Atlan. I just came in here looking for loot. Maybe some electrum, if anyone left any lying around. Now I'm just trying to get off this godsforsaken ship.
    But then I died. And now I'm back.
    <<SetSpeaker Stellan neutral ???>>
    You're nothing but a common thief, yet you...
    <<SetSpeaker Player>>
    Listen, friend. You can't fault a man for trying to steal from an abandoned wizard's tower. Didn't realize it wouldn't be so abandoned on the inside.
    <<SetSpeaker Stellan>>
    ... I'm Stellan. You must know, whatever ambition brought you here, whatever misguided arrogance that you could handle whatever threats might lurk within...
    Your apparent success against <i>him</i>?
    <<SetSpeaker Stellan sad>>
    It was only a fluke. You're stuck here. Just like the rest of us.
<<elseif $headNode is "KilledByTimeLich">>  // If you made it to the lich but died on run 1
    <<SetSpeaker Stellan neutral ???>><<Emote Stellan question>>
    How did you reach him...?
    <<SetSpeaker Player>><<Emote Player surprise>>
    <i>Uugh</i>... I'm... alive?
    <<SetSpeaker Stellan neutral ???>>
    Yes, yes, it's a time loop. How miraculous and novel. Even death won't set us free. Now get on your feet and quit looking so ill. Who are you and what are you doing on this ship?
    <<SetSpeaker Player>>
    Name's Atlan. Came in here looking for loot. Maybe some electrum, if anyone left any lying around. Now I'm just trying to get off this godsforsaken ship.
    <<SetSpeaker Stellan neutral ???>>
    Ah, yes. The wayward adventurer. You do look like a pirate. Perhaps a <i>scoundrel,</i> if you prefer. And don’t you correct me by claiming to be a “trader” or “privateer,” as if those words mean anything besides “common thief.”
    <<SetSpeaker Player>>
    Can't fault a man for trying to steal from an abandoned wizard's tower. Didn't realize it wouldn't be so abandoned on the inside.
    <<SetSpeaker Stellan neutral ???>>
    Nevertheless, it was ambition that brought you here. The misguided arrogance that you could handle whatever threats might lurk within.
    <<SetSpeaker Stellan sad ???>>
    And now, you're stuck. Just like the rest of us.
<<else>>
    <<SetSpeaker Stellan neutral ???>>
    Well, you certainly don’t belong here.
    <<SetSpeaker Player>><<Emote Player surprise>>
    <i>Uugh</i>... I'm... alive?
    <<SetSpeaker Stellan neutral ???>>
    Yes, yes, it's a time loop. How miraculous and novel. Even death won't set us free. Now get on your feet and quit looking so ill. Who are you and what are you doing on this ship?
    <<SetSpeaker Player>>
    Name's Atlan. Came in here looking for loot. Maybe some electrum, if anyone left any lying around. Now I'm just trying to get off this godsforsaken ship.
    <<SetSpeaker Stellan neutral ???>>
    Ah, yes. The wayward adventurer. You do look like a pirate. Perhaps a <i>scoundrel,</i> if you prefer. And don’t you correct me by claiming to be a “trader” or “privateer,” as if those words mean anything besides “common thief.”
    <<SetSpeaker Player>>
    Can't fault a man for trying to steal from an abandoned wizard's tower. Didn't realize it wouldn't be so abandoned on the inside.
    <<SetSpeaker Stellan neutral ???>>
    Nevertheless, it was ambition that brought you here. The misguided arrogance that you could handle whatever threats might lurk within.
    <<SetSpeaker Stellan sad ???>>
    And now, you're stuck. Just like the rest of us.
<<endif>>
===

title: StellanDefaultDialogue1
tags:
---
<<if Visited("StellanDefaultDialogue1")>>
    [[StellanDefaultDialogue2]]
<<elseif $firstClearRunNumber is 1>>
    <<SetSpeaker Stellan>>
    Back again? Unsurprising. Just as I told you: You're stuck here.
    <<SetSpeaker Player>>
    <i>Uugh</i>... What happened to me out there? I felt... different as soon as I left this lounge.
    <<SetSpeaker Stellan>>
    A side effect of the time loop. Everything - even the core essence of a person - is... twisted, within this ship. Continuously, with each reset.
    If you're determined to keep dying out there, I may be willing to help in your endeavor to escape, for a cost. A, ah, mutually beneficial relationship.
    I’m looking for Star Shards, and if you’re going beyond this lounge, you may be able to retrieve some for me. In turn… I have much to offer using the resources you collect on my behalf.
<<elseif $firstClearRunNumber is 2>>    // This dialogue right after first clear
    <<SetSpeaker Stellan neutral ???>><<Emote Stellan question>>
    How did you...?
    <<SetSpeaker Player>>
    <i>Uugh</i>... What happened to me out there? I felt... different as soon as I left this lounge.
    <<SetSpeaker Stellan neutral ???>>
    A side effect of the time loop, the core essence of a person being continuously twisted with each reset.
    Now, tell me. How did you <i>do</i> that?
    <<SetSpeaker Player>>
    What, kill the skeleton man?
    <<SetSpeaker Stellan neutral ???>>
    Lich. You killed the Lich.
    <<SetSpeaker Stellan>>
    ... I’m Stellan. If you're determined to keep dying out there, I may be willing to help in your endeavor to escape, for a cost. A, ah, mutually beneficial relationship.
    I’m looking for Star Shards, and if you’re going beyond this lounge, you may be able to retrieve some for me. In turn… I have much to offer using the resources you collect on my behalf.
<<else>>
    <<SetSpeaker Stellan neutral ???>>
    Back again? Unsurprising. Just as I told you: You're stuck here.
    <<SetSpeaker Player>>
    <i>Uugh</i>... What happened to me out there? I felt... different as soon as I left this lounge.
    <<SetSpeaker Stellan neutral ???>>
    A side effect of the time loop. Everything - even the core essence of a person - is... twisted, within this ship. Continuously, with each reset.
    <<SetSpeaker Stellan>>
    I’m Stellan. If you're determined to keep dying out there, I may be willing to help in your endeavor to escape, for a cost. A, ah, mutually beneficial relationship.
    I’m looking for Star Shards, and if you’re going beyond this lounge, you may be able to retrieve some for me. In turn… I have much to offer using the resources you collect on my behalf.
<<endif>>
===

title: StellanDefaultDialogue2
tags:
---
<<if Visited("StellanDefaultDialogue2")>>
    [[StellanDefaultDialogue3]]
<<elseif $firstClearRunNumber > 0 and $firstClearRunNumber < 3>>
    <<SetSpeaker Stellan>>
    The inevitability of your death will continue if you don't stop being so careless.
    <<SetSpeaker Player angry>><<Emote Player angry>>
    Well, at least I'm doing <i>something</i> instead of wallowing in self-pity.
    <<SetSpeaker Player>>
    Make yourself useful and help me understand what's going on here. Who is the Captain of this ship? It didn't even ping on my radar, I assumed it was abandoned.
    <<SetSpeaker Stellan sad>>
    The Captain?
    <<SetSpeaker Stellan>>
    The Wizard of this tower is long dead. I was once his apprentice. But the Lich rules this ship now.
    <<SetSpeaker Player angry>>
    Then why hasn't killing him broken us all out of this godsforsaken loop?
    <<SetSpeaker Stellan>>
    He's a <i>Lich</i>, Atlan. He wields time as a weapon to use against us. Killing him doesn't matter while his loop keeps him from a true death.
<<elseif $firstClearRunNumber is 3>>    // This dialogue right after first clear
    <<SetSpeaker Stellan>><<Emote Stellan question>>
    How did you...?
    <<SetSpeaker Player confused>>
    Who <i>was</i> that? Was he the Captain?
    <<SetSpeaker Stellan sad>>
    The Captain?
    <<SetSpeaker Stellan>>
    The Wizard of this tower is long dead. I was once his apprentice. But the Lich rules this ship now.
    <<SetSpeaker Player angry>><<Emote Player angry>>
    Then why didn't killing him break us all out of this godsforsaken loop?
    <<SetSpeaker Stellan>>
    He's a <i>Lich</i>, Atlan. He wields time as a weapon to use against us. Killing him doesn't matter while his loop keeps him from a true death.
<<else>>
    <<SetSpeaker Stellan>>
    The inevitability of your death will continue if you don't stop being so careless.
    <<SetSpeaker Player angry>><<Emote Player angry>>
    Well, at least I'm doing <i>something</i> instead of wallowing in self-pity.
    <<SetSpeaker Player>>
    Make yourself useful and help me understand what's going on here. Who is the Captain of this ship? It didn't even ping on my radar, I assumed it was abandoned.
    <<SetSpeaker Stellan sad>>
    The Captain?
    <<SetSpeaker Stellan>>
    The Wizard of this tower is long dead. I was once his apprentice. But the Lich rules this ship now.
    <<SetSpeaker Player>>
    What a dramatic way of saying he’s the one I have to kill to break us all out of this godsforsaken loop.
<<endif>>
===

title: StellanDefaultDialogue3
tags:
---
<<if Visited("StellanDefaultDialogue3")>>
    [[StellanDefaultDialogue4]]
<<else>>
    <<SetSpeaker Player>>
    If you’ve got all this fancy magic to make me stronger, why don’t you just do it all at once? Make me unstoppable and send me out to conquer this ship?
    <<SetSpeaker Stellan>>
    Well, first of all, you would die. Without time to adjust in increments, your body would surely burn up from that much arcane energy at once.
    <<SetSpeaker Player>>
    But we’re stuck in a time loop – I’d just come right back.
    <<SetSpeaker Stellan>><<Emote Stellan angry>>
    Yes. Without the magic having taken hold. Thus wasting our limited supply of Star Shards – the second limiting factor here, as I can only do so much as the resources you bring me allow.
    <<SetSpeaker Stellan>>
    And finally… because I can’t turn you into a <i>god,</i> Atlan. I can only enhance or unlock hidden potential. Believe me. There are boundaries of the arcane that should not be crossed.
    <<SetSpeaker Stellan sad>>
    If the Lich has taught me anything, it's to learn that lesson before it's too late.
<<endif>>
===

title: StellanDefaultDialogue4
tags:
---
<<if Visited("StellanDefaultDialogue4")>>
    [[StellanDefaultDialogue5]]
<<else>>
    <<SetSpeaker Stellan>>
    Back so soon? Yet you walked out of here with such confidence. Surely you’re more capable than that pathetic display.
    <<SetSpeaker Player>>
    Just getting warmed up.
    <<SetSpeaker Stellan>>
    Sure. Either that, or you’re in way over your head.
<<endif>>
===

title: StellanDefaultDialogue5
tags:
---
<<if Visited("StellanDefaultDialogue5")>>
    [[StellanDefaultDialogue6]]
<<else>>
    <<SetSpeaker Stellan>>
    Anything new to report? Any progress whatsoever?
    <<SetSpeaker Player>>
    Friend, am I mistaken, or could that be a tinge of hope in your voice?
    <<SetSpeaker Stellan>>
    Surely not. Merely trying to remind you of how utterly futile your attempts are.
    <<SetSpeaker Stellan sad>>
    Believe me, I would know.
<<endif>>
===

title: StellanDefaultDialogue6
tags:
---
<<if Visited("StellanDefaultDialogue6")>>
    [[StellanDefaultDialogue7]]
<<else>>
    <<SetSpeaker Stellan>>
    Giving up yet?
    <<SetSpeaker Player>>
    Never.
    <<SetSpeaker Stellan sad>>
    … You will.
    <<SetSpeaker Player>>
    That a threat, friend?
    <<SetSpeaker Stellan sad>>
    A resignation.
<<endif>>
===

title: StellanDefaultDialogue7
tags:
---
<<if Visited("StellanDefaultDialogue7")>>
    [[StellanDefaultDialogue8]]
<<else>>
    <<SetSpeaker Player>>
    Stellan, you’ve seemed to indicate to me in the past that you may have tried to escape this place on your own? Perhaps even… break whatever loop exists here?
    <<SetSpeaker Stellan sad>>
    Ah. Yes, well…
    <<SetSpeaker Stellan>>
    If anyone on this ship were to know what a hopeless situation you are in, it would be me.
    <<SetSpeaker Player>>
    If you’ve tried what I am attempting, surely there’s some sort of information you could share with me about what I’m up against?
    <<SetSpeaker Stellan>>
    For now, I think the most help I can be is offering up my magic.
<<endif>>
===

title: StellanDefaultDialogue8
tags:
---
<<if Visited("StellanDefaultDialogue8")>>
    [[StellanDefaultDialogue9]]
<<else>>
    <<SetSpeaker Player>>
    You seem to know far more about what’s going on here than anyone else I’ve encountered on this ship. No one else even seems to find it particularly pressing that we get out of here.
    <<SetSpeaker Stellan>>
    I’m not certain any of the others you've encountered realize they’re trapped. It’s… complicated.
    The time loop has plucked everyone from a different moment, and when things reset, memories and perceptions of what they’ve been through and how long they’ve gone through it get hazy.
<<endif>>
===

title: StellanDefaultDialogue9
tags:
---
<<if Visited("StellanDefaultDialogue9")>>
    [[StellanDefaultDialogue10]]
<<else>>
    <<SetSpeaker Stellan>>
    Had enough yet?
    <<SetSpeaker Player>>
    What’s one more death to a man who could face countless more before finally facing the gods?
    <<SetSpeaker Stellan>><<Emote Stellan angry>>
    Atlan, you are a scoundrel and a fool and one day you are going to get yourself killed enough times that those very gods will board this ship and drag you to the afterlife with their own hands for making such a mockery of their law.
    Until that day… What can I do to help?
<<endif>>
===

title: StellanDefaultDialogue10
tags:
---
<<if Visited("StellanDefaultDialogue10")>>
    [[StellanDefaultDialogue11]]
<<else>>
    <<SetSpeaker Stellan>>
    Tell me, has the Captain's Log been a valuable resource for you?
    <<SetSpeaker Player>><<Emote Player surprise>>
    What, this? Thought it was junk. I was keeping it on me to sell as a vintage antique when I leave this place-
    <<SetSpeaker Stellan>>
    No need to get defensive, Atlan. If I had a problem with you pocketing my old mentor's journal for yourself, I would have brought it up much sooner.
    In fact, it's been rather convenient for me. When I feel like it, I can use it to track your progress through the ever-changing chambers of this ship.
    <<SetSpeaker Player>>
    I don't know whether to be concerned or flattered.
<<endif>>
===

title: StellanDefaultDialogue11
tags:
---
<<SetSpeaker Player>><<BranchComplete DefaultDialogue DefaultDialogue>>
So this Captain's Log. It belonged to your mentor, before... all this?
What was he like?
<<SetSpeaker Stellan>>
Stubborn. He... spent more time telling me what I <i>couldn't</i> do than teaching me what I <i>could</i>.
His wisdom was immeasurable.
<<SetSpeaker Player confused>>
... Losing him must have been hard on you.
<<SetSpeaker Stellan sad>>
...
<<SetSpeaker Stellan>>
The hardest part is knowing a loss was preventable, if only different choices had been made.
===



title: StellanEpilogueDialogue
tags:
---
<<SetSpeaker Player confused>>
Play recording.
<<SetSpeaker Stellan comm>>
Hello, friend. A message for you, before you finally leave this place for good.
<<SetSpeaker Stellan commSad>>
I… Hmm.
Where to begin?
<<SetSpeaker Stellan comm>>
I, like everyone on this ship, was plucked from a very specific moment in my life. A defining moment, for me. A… a crossroads.
Only I, unlike the others, had the unique opportunity to see how one of the possible choices that lay before me would play out if -- when -- I chose it.
<<SetSpeaker Stellan commSad>>
How it <i>did</i> play out. In my future.
<<SetSpeaker Stellan comm>>
You didn't recognize me in him, did you? Or perhaps you did, you just didn't want to admit it. To me or to yourself.
Admittedly, I cannot be sure which is worse: Not a trace of me remaining within him…
<<SetSpeaker Stellan commSad>>
... Or that monster being, still, unmistakenly me. <i>Me</i> that <i>monster</i>.
<<SetSpeaker Stellan comm>>
In all my time for endless contemplation here in this prison, I never could have predicted you getting stuck in here with all of us. A hell of my own creation, yet still you managed to break in and set us all free.
Perhaps my mother was right, after all. Perhaps the caelarae were watching over me, yet.
You died too much, suffered too much for my punishment. No amount of suffering could ever bring me close to enough to repay you for what you've done here, but…
I can only hope, through all of this, that you know I have suffered, too, watching you fail time and time again. To know that with each of your deaths, the blood was always on my own hands.
<<SetSpeaker Stellan commSad>>
...
<<SetSpeaker Stellan comm>>
… I understand if you can never find it in yourself to forgive me. I would not wish upon you the burden of another task I myself have yet found insurmountable.
But I hope -- I do hope -- that you will know the deepest extents of my gratitude for putting an end to all of this.
<<SetSpeaker Stellan commSad>>
… Thank you, friend. For giving me the chance to atone.
<<SetSpeaker Player confused>>
...
===



title: StellanRepeatable
tags:
---
// Get a random number 0 - 7 and go to that node
<<set $num to RandomNum(8)>>
<<if $num is 0>>
    <<SetSpeaker Stellan>>
    Back again?
<<elseif $num is 1>>
    [[StellanRepeatable1]]
<<elseif $num is 2>>
    [[StellanRepeatable2]]
<<elseif $num is 3>>
    [[StellanRepeatable3]]
<<elseif $num is 4>>
    [[StellanRepeatable4]]
<<elseif $num is 5>>
    [[StellanRepeatable5]]
<<elseif $num is 6>>
    [[StellanRepeatable6]]
<<else>>
    [[StellanRepeatable7]]
<<endif>>
===

title: StellanRepeatable1
tags:
---
<<SetSpeaker Stellan>>
Still not giving up?
===

title: StellanRepeatable2
tags:
---
<<SetSpeaker Stellan>>
Time for a change?
===

title: StellanRepeatable3
tags:
---
<<SetSpeaker Stellan>>
Have you considered it may be time to accept your fate?
===

title: StellanRepeatable4
tags:
---
<<SetSpeaker Stellan>>
More Star Shards? I’ll see what I can do this time.
===

title: StellanRepeatable5
tags:
---
<<SetSpeaker Stellan>>
Can you hurry it along? I’m getting bored of this.
===

title: StellanRepeatable6
tags:
---
<<SetSpeaker Stellan>>
Can’t say I’m surprised to see you here again.
===

title: StellanRepeatable7
tags:
---
<<SetSpeaker Stellan>>
Are you sick of this endless existence yet?
===