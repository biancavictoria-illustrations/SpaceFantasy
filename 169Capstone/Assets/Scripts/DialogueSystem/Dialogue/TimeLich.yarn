title: TimeLichStart
tags:
---
<<if HasDeusExMachina() and not Visited("TimeLichHasDeusExMachina")>>
    [[TimeLichHasDeusExMachina]]
<<endif>>

<<set $visitedDefault to Visited("TimeLichDefaultDialogue")>>
<<set $visitedDefault1 to Visited("TimeLichDefaultDialogue1")>>
<<set $visitedDefault2 to Visited("TimeLichDefaultDialogue2")>>
<<if $visitedDefault and $visitedDefault1 and $visitedDefault2>>
    [[TimeLichNodeSelectHub]]
<<elseif $visitedDefault1>>
    [[TimeLichDefaultDialogue2]]
<<elseif $visitedDefault>>
    [[TimeLichDefaultDialogue1]]
<<else>>
    [[TimeLichDefaultDialogue]]
<<endif>>
===

title: TimeLichNodeSelectHub
tags:
---
<<set $headNode to SelectNextNode()>>

// === Enemy Killed ===

<<if $headNode is "EnemyKilledTimeLich">>
    [[TimeLichEnemyKilledTimeLich]]
<<elseif $headNode is "EnemyKilledBeetleBoss">>
    [[TimeLichEnemyKilledBeetleBoss]]

// === Killed By ===

<<elseif $headNode is "KilledBySlime">>
    [[TimeLichKilledBySlime]]  

// === Generic Stuff ===

<<elseif $headNode is "NumRuns">>
    [[TimeLichNumRuns]]
<<elseif $headNode is "LowHealth">>
    [[TimeLichLowHealth]]
<<elseif $headNode is "BarterFail">>
    [[TimeLichBarterFail]]

<<elseif not Visited("TimeLichLowWis") and Stat("WIS") < 10>>   // Special
    [[TimeLichLowWis]]

<<elseif $headNode is "DefaultDialogue">>
    [[TimeLichDefaultDialogue]]
<<elseif $headNode is "Repeatable">>
    [[TimeLichRepeatable]]

<<else>>
    [[TimeLichRepeatable]]
    ERROR: No head node found for {$headNode}!
<<endif>>
===



title: TimeLichEnemyKilledTimeLich
tags:
---
// <<if Visited("TimeLichEnemyKilledTimeLich")>>
//     [[TimeLichEnemyKilledTimeLich1]]
// <<else>>
<<SetSpeaker Player angry>><<Emote Player angry>><<BranchComplete EnemyKilled EnemyKilledTimeLich>>
<i>How?</i> How are you still here? How is all of this still happening? I <i>killed</i> you!
<<SetSpeaker TimeLich>>
Did I not tell you your attempts were futile?
<<SetSpeaker Player angry>>
I’ve killed you before, I’ll do it again. As many times as it takes.
// <<endif>>
===

title: TimeLichEnemyKilledBeetleBoss
tags:
---
<<SetSpeaker Player>><<BranchComplete EnemyKilled EnemyKilledBeetleBoss>>
That six-legged guard dog of yours… I have to ask, what exactly is it?
<<SetSpeaker TimeLich>>
Ahh… so you have met my enforcer. The Brute Pest is a true testament to what happens when you release innovators from their supposed bonds and let ambition run wild.
A magnificent beast, is it not?
<<SetSpeaker Player>>
Not the word I would use. <i>Nightmare-fuel,</i> maybe. But I guess we all have our interests.
===



title: TimeLichKilledBySlime
tags:
---
<<SetSpeaker TimeLich>><<BranchComplete KilledBy KilledBySlime>>
You were bested by a mere slime last time you fought through my domain, yet still you think yourself strong enough to face me?
<<SetSpeaker Player>>
Those slimes are surprisingly vicious. You’re a frail old man. Yeah, I still think I can take you.
===



title: TimeLichNumRuns
tags:
---
<<set $currentRunNum to GetCurrentRunNum()>>
<<if $currentRunNum > 19>>
    [[TimeLichNumRuns20]]
<<else>>
    <<if Visited("TimeLichNumRuns")>>
        [[TimeLichRepeatable]]
    <<else>>
        <<SetSpeaker TimeLich>>
        Back once more.
        <<SetSpeaker Player>>
        As many times as it takes.
    <<endif>>
<<endif>>
===

title: TimeLichNumRuns20
tags:
---
<<SetSpeaker TimeLich>><<BranchComplete NumRuns NumRuns>>
How many times must we do this before you accept your fate?
<<SetSpeaker Player>>
Getting tired, old man?
<<SetSpeaker TimeLich>>
Never. I have all the time in the universe. Yours, however, is running short.
===



title: TimeLichLowHealth
tags:
---
<<if Visited("TimeLichLowHealth")>>
    [[TimeLichLowHealth1]]
<<else>>
    <<SetSpeaker TimeLich>>
    You approach on the verge of death, yet still you dare to face me? Your arrogance may be unmatched.
    <<SetSpeaker Player>>
    Don’t worry.
    <<SetSpeaker Player confused>><<Emote Player sad>>
    <i>Eugh...</i>
    <<SetSpeaker Player>>
    I’m sure that’s one area where you’ve still got me beat.
<<endif>>
===

title: TimeLichLowHealth1
tags:
---
<<set $num to RandomNum(4)>>
<<if $num is 0>>
    <<SetSpeaker TimeLich>>
    By the looks of you, this won’t take long.
<<elseif $num is 1>>
    <<SetSpeaker TimeLich>>
    You crawl into my chamber, bloody, beaten, <i>broken.</i> I will grant you mercy. The mercy of release.
<<elseif $num is 2>>
    <<SetSpeaker TimeLich>>
    You are already dying, yet once again you subject yourself to this torment. Fine, then. You shall go no further.
<<elseif $num is 3>>
    <<SetSpeaker TimeLich>>
    Already, you bleed. No one is here to help you now – you will die alone. It is only a matter of time.
<<endif>>
===



title: TimeLichDefaultDialogue
tags:
---
<<if Visited("TimeLichDefaultDialogue")>>
    [[TimeLichDefaultDialogue1]]
<<elseif GetCurrentRunNum() is 1>>
    <<SetSpeaker TimeLich neutral ???>>
    Well, you certainly don’t belong here.
    A wayward adventurer. A young hero wreaking havoc in a ship, making it so far only to die.
    <<SetSpeaker Player>>
    I’ve never considered myself much of a hero. Just an opportunist.
    But you must be the Captain – or do you prefer Wizard?
    <<SetSpeaker TimeLich neutral ???>>
    The Captain? ... Oh, young hero, you know nothing of what is going on here.
    <<SetSpeaker TimeLich>>
    I am the Time Lich, and this is my domain.
    <<SetSpeaker Player>>
    Are you aware that your domain has been overrun with slimes? Nasty little things. Don’t worry, I cleaned up a bit for you.
    <<SetSpeaker TimeLich>>
    You will find I am not so easy to dispose of, charlatan. Now, come. It is time for us to begin.
<<else>>
    <<SetSpeaker TimeLich>>
    Ahh... the wayward adventurer.
    How sad, for such a young hero to have made it this far only to die.
    <<SetSpeaker Player>>
    I’ve never considered myself much of a hero. Just an opportunist.
    But you must be the Captain – or do you prefer Wizard? Nice tower you’ve got here.
    Are you aware it’s been overrun with slimes? Nasty little things. Don’t worry, I cleaned up a bit for you.
    <<SetSpeaker TimeLich>>
    You will find I am not so easy to dispose of, charlatan. Now, come. It is time for us to begin.
<<endif>>
===

title: TimeLichDefaultDialogue1
tags:
---
<<if Visited("TimeLichDefaultDialogue1")>>
    [[TimeLichDefaultDialogue2]]
<<else>>
    <<SetSpeaker TimeLich>>
    Your misguided altruism will get you nowhere. You cannot best me.
    <<SetSpeaker Player>>
    Altruism has nothing to do with it, Cap’n. You see, I just couldn’t pass up the temptation of looting this fine spacecraft.
    As soon as I stepped foot inside, I got stuck like the rest of your crew. I’m just trying to save my own skin.
    <<SetSpeaker TimeLich>>
    Perhaps. Whatever you tell yourself of your motivation, your attempts are futile.    
<<endif>>
===

title: TimeLichDefaultDialogue2
tags:
---
<<if Visited("TimeLichDefaultDialogue2")>>
    [[TimeLichDefaultDialogue3]]
<<else>>
    <<SetSpeaker TimeLich>>
    I know who you are, young adventurer. I know everything about you.
    <<SetSpeaker Player>>
    You might control this ship and all your monsters inside it, but I’m not one of your minions.
    <<SetSpeaker TimeLich>>
    Oh? Aren’t you?
<<endif>>
===

title: TimeLichDefaultDialogue3
tags:
---
<<if Visited("TimeLichDefaultDialogue3")>>
    [[TimeLichDefaultDialogue4]]
<<else>>
    <<SetSpeaker TimeLich>>
    You will give up eventually, young hero. I am the master of time. Your attempts may be persistent, but they will be futile.
    <<SetSpeaker Player>>
    I’ll laugh at the irony later, when those were your last words.
<<endif>>
===

title: TimeLichDefaultDialogue4
tags:
---
<<if Visited("TimeLichDefaultDialogue4")>>
    [[TimeLichDefaultDialogue5]]
<<else>>
    <<SetSpeaker Player>>
    Your time is up, Cap’n. I’m about to take this ship from you.
    <<SetSpeaker TimeLich>>
    Bold words from one who does not realize how very wrong he is. I have all the time in the universe.
<<endif>>
===

title: TimeLichDefaultDialogue5
tags:
---
<<if Visited("TimeLichDefaultDialogue5")>>
    [[TimeLichDefaultDialogue6]]
<<else>>
    <<SetSpeaker TimeLich>><<Emote TimeLich question>>
    Again? Why? Why subject yourself to death after death?
    <<SetSpeaker Player>>
    I tend to do my best work when the odds are against me.
    <<SetSpeaker TimeLich>>
    You are a fool. But no matter. You will grow tired of this existence and come to understand the futility of your attempts.
    But take your time, if you must. I have plenty of it to spare.
<<endif>>
===

title: TimeLichDefaultDialogue6
tags:
---
<<if Visited("TimeLichDefaultDialogue6")>>
    [[TimeLichDefaultDialogue7]]
<<else>>
    <<SetSpeaker Player>>
    Time’s up, Lich.
    <<SetSpeaker TimeLich>>
    I am the god of this ship. You are nothing.
    Nothing but a pirate. A scoundrel. A vagabond. <i>Nothing.</i>
    You will not usurp me.    
<<endif>>
===

title: TimeLichDefaultDialogue7
tags:
---
<<if Visited("TimeLichDefaultDialogue7")>>
    [[TimeLichDefaultDialogue8]]
<<else>>
    <<SetSpeaker TimeLich>>
    I sense a familiar arcane trace on you. How… fascinating, to learn that even <i>he</i> has lent his aid.
    <<SetSpeaker Player>>
    Everyone on this ship wants you gone. They’re <i>all</i> helping me. Your whole crew has betrayed you, Cap’n - even your own apprentice.
    <<SetSpeaker TimeLich>>
    How endearing your ignorance is, at times.
    No matter. His betrayal is meaningless, just as your attempts are futile.
<<endif>>
===

title: TimeLichDefaultDialogue8
tags:
---
<<SetSpeaker TimeLich>><<BranchComplete DefaultDialogue DefaultDialogue>>
You must know… I remember you.
<<SetSpeaker Player>>
I would hope so, after all our fights. Would hate to think I hadn’t made an impression by now.
<<SetSpeaker TimeLich>>
No… No, Atlan. I <i>remember</i> you.
<<SetSpeaker TimeLich angry>>
<i>Give up.</i>
<<SetSpeaker Player>>
You claim to know me. You know I won’t give up.
<<SetSpeaker TimeLich angry>>
… Come, then. If we must.
===



title: TimeLichBarterFail
tags:
---
<<SetSpeaker Player angry>><<BranchComplete BarterFail BarterFail>>
You have the power to stop this. What do you have to gain from keeping everyone trapped here?
<<SetSpeaker TimeLich>>
You have resorted to begging. How pathetic.
And I was just beginning to respect your determination.
===



title: TimeLichLowWis
tags:
---
<<SetSpeaker TimeLich>>
You’ll not be a quitter if you submit. Indeed, you would be wise to accept the fate time has written for you.
<<SetSpeaker Player>>
I’m not feeling particularly wise today.
===



title: TimeLichHasDeusExMachina
tags:
---
<<SetSpeaker TimeLich angry>><<Emote TimeLich question>>
Where- where did you get that?
<<SetSpeaker Player>>
What, this hourglass? Looks a lot like the one on top of your staff.
A friend gave it to me.
<<SetSpeaker TimeLich>>
He is no friend of yours, to give you <i>that</i> only to send you to face me once more.
<<SetSpeaker TimeLich angry>><<Emote TimeLich angry>>
He must have known I would take out this insult on <i>you</i>.
// TODO: More of these?
===



title: TimeLichRepeatable
tags:
---
// Get a random number 0 - 4 and go to that node
<<set $num to RandomNum(6)>>
<<if $num is 0>>
    <<SetSpeaker TimeLich>>
    Again?
    <<SetSpeaker Player>>
    Again.
    <<SetSpeaker TimeLich>>
    Fine, then. Let’s get this over with.
<<elseif $num is 1>>
    [[TimeLichRepeatable1]]
<<elseif $num is 2>>
    [[TimeLichRepeatable2]]
<<elseif $num is 3>>
    [[TimeLichRepeatable3]]
<<elseif $num is 4>>
    [[TimeLichRepeatable4]]
<<else>>
    [[TimeLichRepeatable5]]
<<endif>>
===

title: TimeLichRepeatable1
tags:
---
<<SetSpeaker TimeLich>>
Will you never learn?
<<SetSpeaker Player>>
I’ve never been particularly fond of learning.
===

title: TimeLichRepeatable2
tags:
---
<<SetSpeaker TimeLich>>
Give up, you pathetic parasite.
<<SetSpeaker Player>>
Not this time.
===

title: TimeLichRepeatable3
tags:
---
<<SetSpeaker TimeLich>>
Still not giving up?
===

title: TimeLichRepeatable4
tags:
---
<<SetSpeaker TimeLich>>
Back again? Fine, then. <i>Again.</i>
===

title: TimeLichRepeatable5
tags:
---
<<SetSpeaker TimeLich>>
I have all the time in the universe. But you... Your time is running short.
===